*vim-addon-manager.txt* Declarative package manager for Vim
==============================================================================
CONTENTS                                          *vim-addon-manager-contents*

    0. GETTING STARTED & LOOKING FOR HELP <<

    1. Intro                           |vim-addon-manager-intro|
    2. Installation                    |vim-addon-manager-installation|
        2.1 Example: personal          |vim-addon-manager-complex-setup-sample|
            configurable setup,
    3. Functionality provided          |vim-addon-manager-functionality|
        3.1. Commands                  |vim-addon-manager-commands|
        3.2. Functions                 |vim-addon-manager-functions|
    4. Options                         |vim-addon-manager-options|
    5. Installing plugins              |vim-addon-manager-install-plugins|
    6. Uninstalling plugins            |vim-addon-manager-uninstall-plugins|
    7. Addon-info file                 |vim-addon-manager-addon-info|
    8. Author, credits, some notes     |vim-addon-manager-related|
    9. Testing this plugin             |vim-addon-manager-testing|
   10. Some notes for windows users    |vim-addon-manager-windows|
   11. Some notes for Gentoo users     |vim-addon-manager-gentoo|
   12. Troubleshooting and known bug   |vim-addon-manager-trouble-shooting|
       and TODOs as well as "I want .."
   13. VAM vs ...

   20. making plugins work with VAM
        (rtp different from ~/.vim)

==============================================================================

0. GETTING STARTED & LOOKING FOR HELP - something doesn't work

Getting started fast: ~
Read: |vim-addon-manager-installation| and |vim-addon-manager-install-plugins|


NEED HELP: ~
Join irc.freenode.net, /join #vim. Ask there. VAM has many users
MarcWeber is hanging around often so ping him or create a github ticket [1] and
people will try to help you. You should skim the docs before asking for
help though. Also see |vim-addon-manager-author|
[1] https://github.com/MarcWeber/vim-addon-manager/issues

==============================================================================
1. Intro                                             *vim-addon-manager-intro*

Consider reading |plugin| (follow this link by g ctlr-]) to understand the
manualy way of installing plugins. VAM helps keeping ~/.vim clean by
separating plugins from each other.

Features:

    - Separate directories for each plugins
    - Dependency resolution
    - Popular VCS support: plugin supports fetching from Git, Mercurial,
      Subversion and Bazaar repositories

Dependencies:
    - Curl, wget or other program that can output URL contents to stdout (in
      order to get http protocol support)
    - Git, Mercurial, Subversion and Bazaar (if you want to install plugins
      from appropriate repositories)
    - vcs_checkouts plugin (comes with this addon)
    - Either tar, gzip and zip or 7-zip (required for unpacking some addons)
    - Vimball plugin (required for installing some addons) (in latest vim it
      is included by default)

What does "Declarative package manager" mean? The final behaviour of Vim
should be declared once. Your ~/.vimrc and :UpdateAddons should be enough
to cause same Vim behaviour everywhere. [2]

==============================================================================
2. Installation                               *vim-addon-manager-installation*

Windows users: see |vim-addon-manager-windows|.
Gentoo users: see |vim-addon-manager-gentoo|.
linux/cygwin/OSX (anything support /bin/sh like shells) users:
Just uncomment lines starting with "unix based os" below and Vim will checkout
VAM.

Add the following to your .vimrc and uncomment the lines for unix >

        fun SetupVAM()
          " YES, you can customize this vam_install_path path and everything still works!
          let vam_install_path = expand('$HOME') . '/.vim/vim-addons'
          exec 'set runtimepath+='.vam_install_path.'/vim-addon-manager'

          " * unix based os users may want to use this code checking out VAM
          " * windows users want to use http://mawercer.de/~marc/vam/index.php
          "   to fetch VAM, VAM-known-repositories and the listed plugins
          "   without having to install curl, unzip, git tool chain first
          if !isdirectory(vam_install_path.'/vim-addon-manager') && 1 == confirm("git clone VAM into ".vam_install_path."?","&Y\n&N")
            " I'm sorry having to add this reminder. Eventually it'll pay off.
            call confirm("Remind yourself that most plugins ship with documentation (README*, doc/*.txt). Its your first source of knowledge. If you can't find the info you're looking for in reasonable time ask maintainers to improve documentation")
            exec '!p='.shellescape(vam_install_path).'; mkdir -p "$p" && cd "$p" && git clone --depth 1 git://github.com/MarcWeber/vim-addon-manager.git'
          endif

          call vam#ActivateAddons([], {'auto_install' : 0})
          " sample: call vam#ActivateAddons(['pluginA','pluginB', ...], {'auto_install' : 0})
          " where pluginA could be github:YourName or snipmate-snippets see vam#install#RewriteName()
          " also see section "5. Installing plugins" in VAM's documentation
          " which will tell you how to find the plugin names of a plugin
        endf
        call SetupVAM()
        " experimental: run after gui has been started (gvim) [3]
        " option1:  au VimEnter * call SetupVAM()
        " option2:  au GUIEnter * call SetupVAM()
        " See BUGS sections below [*]

<   Note: Depending on the option you choose to run ActivateAddons Vim may not be
    able to show the questions correctly asking you to install a plugin.
    If that's the case (for whatever reason) I recommend installing the plugin
    using :ActivateAddons before adding it to the list in .vimrc

    Of course you can also fetch vim-addon-manager and
    vim-addon-manager-known-repositories manually and keep full control.
    Then you have to keep the runtimepath line and call vam#ActivateAddons(..
    in the func above only.

4. Optionally generate helptags (this will be done automatically for all
    plugins which are installed by VAM, but VAM can't install itself obviously): >
       UpdateAddon vim-addon-manager

Tip: Press q once intsead of pressing <cr> each time Vim asks you to press
Enter.

Continue reading |vim-addon-manager-install-plugins|.

------------------------------------------------------------------------------
2.1 Example: personal configurable setup *vim-addon-manager-complex-setup-sample*
>
  call vam#ActivateAddons(["github:YOURNAME"],{'auto_install' : 0})
  " this initializes Vim the way *you* want also loading more plugins:
  call vim_addon_YOURNAME#Activate(['c-dev','ruby-dev'])
<
  My implementation looks like this:
  https://github.com/MarcWeber/vim-addon-MarcWeber/blob/master/autoload/vim_addon_MarcWeber.vim

  You can then load plugins depending on env vars:
  Example: >
  call vim_addon_YOURNAME#Activate(['always']+split($V,','))
< Then you can run vim like this from shell >
  V=c-dev,ruby-dev vim
<
  This section was written to inspire you only.

==============================================================================
3. Functionality provided                    *vim-addon-manager-functionality*

------------------------------------------------------------------------------
3.1. Commands                                     *vim-addon-manager-commands*

InstallAddons {name} ...                                      *:InstallAddons*
        Install addons with given names. See |vam#install#InstallAddons()| for
        more details.
        You use InstallAddons instead of ActivateAddons if you want to review
        plugin source before running it.

ActivateAddons {name} ...                                    *:ActivateAddons*
        Activate addons with given names. See |vam#ActivateAddons()| for
        more details.

ActivateInstalledAddons {name} ...                  *:ActivateInstalledAddons*
        See |:ActivateAddons|, this command is just the same, but completion
        function completes only installed addon names.

UpdateAddons [{name} ...]                                      *:UpdateAddons*
        Update addons with given names. Without arguments updates all addons.
        See |vam#install#Update()| for more details.

UninstallNotLoadedAddons {name} ...                *:UninstallNotLoadedAddons*
        Unistall addons with given names. See
        |vam#install#UninstallAddons()| for more details.

------------------------------------------------------------------------------
3.2. Functions                                   *vim-addon-manager-functions*

vam#ActivateAddons({addons}[, {opts}])                  *vam#ActivateAddons()*
        Installs and activates plugins listed in {addons} (see description of
        |vam#install#Install()|) list alongside with their dependencies. This
        is meant to be called from your .vimrc. Installation takes place only
        if plugin directory does not exist. Activation means adding plugin
        directory to 'runtimepath'. If vim has already started VAM will also
        source plugin/**/*.vim (in other case sourcing is done by vim).

        Optional {opts} argument should be a dictionary with key
        "auto_install". It will be used to override global option
        |vim-addon-manager-auto_install|. Other possible keys are for internal
        use only.

        In order to manually activate plugins try using |:ActivateAddons|
        which also provides completion.

vam#install#Install({addons}[, {opts}])                *vam#install#Install()*
        Installs plugins listed in {addons} list. Each list item must be one
        of the following:
            - Name of the plugin
            - Path to |addon-info.txt| file (it must contain at least one
              forward or back slash, so use `./plugname-addon-info.txt' for
              files located in the current directory)i
            - |addon-info.txt| URL. In this case {arg} must start with
              `http://'.
        After installing the plugin help tags are updated, see |:helptags|.

        {opts} argument is described in |vam#ActivateAddons()|.

vam#install#Update({addons})                            *vam#install#Update()*
        Updates plugins which names are listed in {addons}. If an empty list
        is given, then updates all plugins. vam#install#Update also updates
        the help tags. Note that if plugin is not under control of some VCS,
        it tries to retain user changes unless |vim-addon-manager-do_diff| is
        disabled.

vam#AddonInfo({name})                                        *vam#AddonInfo()*
        Returns dictionary that contains information given in |addon-info.txt|
        file that comes with requested addon. If no |addon-info.txt| file is
        present, it is not readable or addon with given name is not installed,
        then it returns an empty dictionary.

                                              *vam#install#MergePluginFiles()*
vam#install#MergePluginFiles([{name}] [, blacklist-regex ])
        Highly experimental function that may speed up vim start on heavy IO
        load. This function renames all `plugin' directories to
        contents into `~/.vim/after/plugin/vim-addon-manager-merged.vim' which
        should cause less IO stress to your system, thus Vim will startup
        faster. This can scripts because:
            - s:... script global variables are renamed automatically to
              prevent name clashes
            - Guards are replaced by if ... endif which might be inefficient
            - `finish' statement that start line and every line after them are
              just commented out

        Using the blacklist-regex you can filter plugin/*.vim files and
        prevent them from being included. For example this excludes many tlib
        plugins.: >

            let s:merge = [ "tlib" ]
            call vam#ActivateAddons(s:merge)
            command MergePluginFiles call vam#install#MergePluginFiles(s:merge+["tlib"], '\%(cmdlinehelp\|concordance\|evalselection\|glark\|hookcursormoved\|linglang\|livetimestamp\|localvariables\|loremipsum\|my_tinymode\|pim\|quickfixsigns\|scalefont\|setsyntax\|shymenu\|spec\|tassert\|tbak\|tbibtools\|tcalc\|tcomment\|techopair\|tgpg\|tmarks\|tmboxbrowser\|tortoisesvn\|tregisters\|tselectbuffer\|tselectfile\|tsession\|tskeleton\|tstatus\|viki\|vikitasks\)\.vim_merged')
            command UnmergePluginFiles call vam#install#UnmergePluginFiles()
<

        Yes, the info files should be cached as well (TODO)

vam#install#RewriteName({name})                    *vam#install#RewriteName()*
    Returns a dictionary if {name} matches one of the following patterns:
    Pattern            Dictionary ~
    github:{Name}      {"type": "git",
                        "url": "git://github.com/{Name}/vim-addon-{Name}}
    github:{N}/{Repo}  {"type": "git",
                        "url": "git://github.com/{N}/{Repo}"}
    git:{URL}          {"type": "git", "url": {URL}} (experimental)

==============================================================================
4. Options                                         *vim-addon-manager-options*
                                                         *g:vim_addon_manager*

All options are located in the global dictionary g:vim_addon_manager. It also
contains some stuff that user should never modify if he wants to see this
plugin working. Possible keys:

auto_install                                  *vim-addon-manager-auto_install*
        This options disables plugin installation confirmation. It will not
        disable deprecation warnings and other prompts.
plugin_sources                              *vim-addon-manager-plugin_sources*
        This option contains a dictionary where keys are plugin names and
        values are described by |addon-info-repository|. Values defined in
        this dictionary override corresponding values in |addon-info.txt|
        files, so be careful when you extend it.
plugin_root_dir                            *vim-addon-manager-plugin_root_dir*
        Defines a directory where plugins will be installed to. If directory
        where this plugin is installed to (three levels up relative to
        autoload/vam.vim: autoload/../..) is writeable by user, then
        it defaults to directory in question (so, if user has installed
        vim-addon-manager to ~/.vim/bundle/vim-addon-manager, this will be
        equal to ~/.vim/bundle). If autoload/vam.vim is not
        writeable by the user, then it defaults to ~/.vim/vim-addons. Note that you
        must set this variable before calling any vam function.
                             *vim-addon-manager-known_repos_activation_policy*
known_repos_activation_policy
        String, valid values: `ask', `never' and `autoload' (default). Defines
        when |vim-addon-manager-known| plugin should be loaded. `Ask' means
        that user will be prompted when this option is required. `Never' means
        that |vim-addon-manager-known| will not be loaded and the user will
        not get any prompts. `Autoload' means that it will be loaded when it
        is required, user also won't get asked.
known                                                *vim-addon-manager-known*
        String, specifies the name of plugin which provides list of
        repositories. Default is vim-addon-manager-known-repositories.
change_to_unix_ff                        *vim-addon-manager-change_to_unix_ff*
        Remove CR symbols before LF symbols in any vim files (changes dos and
        mixed dos/unix line endings to unix line endings). Is on by default if
        you are on unix (unix, win32unix, macunix), off otherwise.
do_diff                                            *vim-addon-manager-do_diff*
        If this option is true (default), then try to retain user changes to
        installed plugins (unless they are under SCM control). Requires diff
        and patch programs installed.
name_rewriting                              *vim-addon-manager-name_rewriting*
        Set this to a dictionary with functions each taking a plugin name and
        returning either a dictionary with repository location (see
        |addon-info-repository|) for given name or zero. Location will be set
        to the first non-zero value, functions are executed in order
        determined by |sort()|ing |keys()| of the dictionary. These functions
        will be called when repository location for requested plugin is not
        found.
        Value will be extended with >
            {"99git+github": "vam#install#RewriteName"}
<       after it is read (see |vam#install#RewriteName()|).

The following options are used by vim-addon-manager-known-repositories, not by
VAM itself. So if you set |vim-addon-manager-known| to another value they may
have no effect.

scm_merge_strategy                        *vim-addon-manager-scm_merge_stategy*
        String, valid values: `force' (default), `keep' and `never'. Defines,
        whether SCM sources should be used instead of www.vim.org sources:
        `forces' stands for prefering SCM over www.vim.org, `keep' means that
        SCM sources will be used only if the plugin is not available from
        www.vim.org, `never' means that SCM sources will be never used.
MergeSources                                  *vim-addon-manager-MergeSources*
        Function reference, defines a function that will be used to merge
        user, |vim-addon-manager-known-repositories| and SCM sources.
        Overrides |vim-addon-manager-scm_merge_stategy|. Will be called with
        three arguments: user sources (most of time it is an empty
        dictionary), non-SCM sources defined in
        |vim-addon-manager-known-repositories| and SCM sources also defined
        there. If MergeSources function is a dictionary function, it will be
        provided an empty dictionary as |self| dictionary.

        An Example can be found in this file.

==============================================================================
5. Installing plugins                      *vim-addon-manager-install-plugins*

First read |vim-addon-manager-installation|.

As documented |vam#Activate| or |:ActivateAddons| will fetch
(=install) the plugin if it failed to find requested addon in your addons
directory.

I recommend using |:ActivateAddons| to "preview" a plugin and if it both works
and you like it add it to ~/.vimrc.
This also works around the issue with questions not being displayed correctly
at startup [3].

You can use |:ActivateAddons|'s tab completion to find the plugin name fast.

Usually you add the names of the addons this way to your .vimrc: >
   set runtimepath+=~/.vim/vim-addons/vim-addon-manager
   call vam#ActivateAddons(["pluginA","pluginB"])
<
You can use the InstallAddons function to install plugins only.
You should do this if you don't trust foreign code. Then you can review it
before it'll be executed. Otherwise use ActivateAddons

You can install plugins by URL. See |vam#install#Install()|.
I prefer you telling me about your repository location, though.
Also see |vim-addon-manager-repository-locations|.

Implementation details: ~
Vim does not source plugin/*.vim and after/plugin/*.vim automatically
if you add pathes to runtimepath after Vim has startup. vim-addon-manager
takes care of that for you.

addon names & how to find the plugin you're looking for ~
Open the vim-addon-manager-known-repositories/plugin/vim-addon-manager-known-repositories.vim
There you'll find the two types of dictionaries which are used to find and
install plugins. You'll find lines like this:
>
  let s:plugin_sources['Align294'] = {'title': 'Align', 'version': '35/41', 'url': 'http://www.vim.org/scripts/download_script.php?src_id=10110', 'vim_version': '7.0', 'date': '2009-03-04', 'vim_script_nr': 294, 'type': 'archive', 'script-type': 'utility', 'archive_name': 'Align.vba.gz', 'author': 'Charles Campbell'}
  let s:plugin_sources['Align1497'] = {'title': 'Align.vim', 'version': '0.15', 'url': 'http://www.vim.org/scripts/download_script.php?src_id=5451', 'vim_version': '6.0', 'date': '2006-03-17', 'vim_script_nr': 1497, 'type': 'archive', 'script-type': 'utility', 'archive_name': 'Align.vim', 'author': 'Michael Gnezdov'}
<
In this example both titles are stripped to "Align" which would result in a
name collision. Thus the gather script appends the script ids.
The script id can be found in the plugin web url.

There is a second type of dict containing bleeding edge versions:
>
  let s:scm_plugin_sources['tmru'] = {'type': 'git', 'url': 'git://github.com/tomtom/tmru_vim.git' }
<
Both dicts are merged. The default is to prefer the scm version but you can
choose. (-> MergeSources)

The keys (eg tmru, Align294, Align1497) must be used instead of {name}.
>
  :ActivateAddons Align<c-d>[<tab>]
< showes a list of all names containing "Align" which is often enough to
identify the plugin you're looking for.

Maybe a :ShowScriptNameOfSciptId X command should be added (?)

==============================================================================
6. Uninstalling Plugins                  *vim-addon-manager-uninstall-plugins*

Follow these steps:

- Remove the plugin name from the call to |vam#ActivateAddons()| in your
  vimrc.

- Restart Vim and remove plugin directory using >
  :UninstallNotLoadedAddons {pluginname}
< or rm -fr those directories manually.

==============================================================================
7. Addon-info file                              *vim-addon-manager-addon-info*
                                                              *addon-info.txt*

Each plugin that intends to use vim-addon-manager for distributing itself
needs to have addon-info.json [1] file in its root,
that contains the JSON dictionary with the following keys (none of the keys
are required):

[1]:  older alternative name: {plugname}-addon-info.txt

name                                                         *addon-info-name*
        Name of the plugin. Must not contain any characters that cannot be
        used in a directory name (including directory names separator).
        Note that if the value of this key, {plugname} part in addon-info.txt
        file name and directory under which plugin is installed are not equal,
        then user that tries to use your plugin may catch strange bugs.

repository                                             *addon-info-repository*
        Describes where the plugin should be fetched from. Ignored unless the
        plugin is installed using either the second or third form of
        |vam#install#Install()| call (see its description). This is
        a dictionary with the following keys:

         !! Note: As very common in extreme programming documentation may be
         !! outdated. So may be this section. So refer to the code:
         !! |vam#install#Checkout()| to find out about all supported keys
         !! Its easy to read and extend. Contact me if you're unsure. I'll help
         !! you.
         !! I'd even consider moving this section into the code only refering
         |! to it - so that its kept in sync.

        Key      Description ~
        type     Required, must be either one of `hg', `git', `svn', `bzr' or
                 an empty string.
        url      If `type' key contains a VCS name (`hg', `git', `bzr' or `svn'),
                 then this key describes a url that will be passed to this
                 VCS. If `type' key contains an empty string, then it should
                 contain location of the archive.
        script-type
                 One of `plugin', `syntax', or `indent'. The .vim file will be
                 moved into the specific subdirectory.
        archive_name
                 If `type' key contains an empty string, then archive which
                 location is described by the `url' key will be saved in file
                 with name {archive_name} and processed according to its
                 extension. Supported extensions (note that if you have 7-zip
                 installed no other archive tools are required):
                 Extension     How it is handled ~
                 .vim          This extension designates that plugin is
                               a single file. So, if exists key `script-type'
                               and it is equal to `syntax' or `indent', then
                               this file is put under {plugname}/syntax or
                               under {plugname}/indent, otherwise it is put
                               under directory described by
                               {plugname}/{target_dir} (where {target_dir} is
                               a value of `target_dir' key if it is present
                               and `plugin' otherwise).
                .tar.gz, .tgz  This extension designates that plugin is
                               contained inside tar archive compressed by
                               gzip. In this case, archive is uncompressed by
                               `gzip' and unpacked to its directory by `tar'
                               (both utilities may be replaced with `7z x').
                .tar.bz2, .tbz2
                               Like .tar.gz, but with bzip2 compression.
                .tar           This extension designates that plugin is
                               contained inside uncompressed tar archive. In
                               this case, archive is unpacked to its directory
                               by `tar'.
                .zip           This extension designates that plugin is
                               contained inside a zip archive. In this case
                               archive is unpacked to its directory by
                               `unzip' (may be replaced by `7z x').
                .7z, .rar, .cab, .arj, .jar
                               This extension designates that plugin is
                               contained inside an archive that is supported
                               by p7zip archiver. In this case archive is
                               unpacked to its directory by `7z x'.
                .vba           This extension designates that plugin is
                               contained in a vimball archive. In this case
                               vimball#Vimball() function is used, see
                               |:UseVimball| for more details.
                .vba.gz        This extension designates that plugin is
                               contained in a vimball archive compressed by
                               gzip. In this case archive is uncompressed and
                               vimball#Vimball() function is used, see
                               |:UseVimball| for more details.
                .vba.bz2       Like .vba.bz2, but with bzip2 compression.
        deprecated
                If this key is present and contains non-empty string, then
                every time when user tries to install this plugin he will see
                this message and will have to confirm installation.
        strip-components
                Strip first {strip-components} directories after unpacking an
                archive. If it is equal to special value `-1' (default), then
                it will strip either one directory (if archive contains the
                only directory that is not special) or will not strip anything
                at all. Recognized special directories: [ft]plugin, autoload,
                colors, compiler, indent, keymap, lang, print, spell, syntax.

dependencies                                         *addon-info-dependencies*
        Describes plugins that are required for the plugin, must contain
        a dictionary where keys are plugin names and values describe where
        appropriate plugins should be fetched from (overriden by
        |vim-addon-manager-plugin_sources|). The format of the values is the
        same as |addon-info-repository|.


version                                                   *addon-info-version*
author                                                     *addon-info-author*
maintainer                                             *addon-info-maintainer*
description                                           *addon-info-description*
homepage                                                 *addon-info-homepage*
        Version, author, maintainer, description and plugin homepage. Ignored,
        but setting them to the real values will not do any harm.

==============================================================================
8. Author, credits, some notes                     *vim-addon-manager-related*

                                      *vim-addon-manager-repository-locations*
List of known repositories is defined in a separate plugin called
|vim-addon-manager-known-repositories|. If you want your plugin to be added to
this list, then contact the author. Note that this list is also populated
automatically from www.vim.org site, so sometimes you do not need to do
anything.

As alternative you can specify them in your .vimrc like what that plugin is
doing: Add your own dicts to: >
    let g:vim_addon_manager = {}
    let g:vim_addon_manager['plugin_sources'] = {}
    let g:vim_addon_manager['plugin_sources']['your-plugin-name'] = { ... }

<
Eg if you want to patch -known-repository sources try this: >
    fun MyMergeSources(plugin_sources, www_vim_org, scm_plugin_sources)
      " run default:
      call vam_known_repositories#MergeSources(a:plugin_sources, a:www_vim_org, a:scm_plugin_sources)

      " patch sources the way you like. This example adds username and password
      " for SVN repositories. As alternative you could overwrite git urls etc ..
      for your_plugin in ['svn-driven-key1', ...]
        let a:plugin_sources[your_plugin]['username'] = 'svn user'
        let a:plugin_sources[your_plugin]['password'] = 'svn user'
       endfor
    endf
    " tell VAM to use your MergeSources function:
    let g:vim_addon_manager = {}
    let g:vim_addon_manager['MergeSources'] = function('MyMergeSources')

<
In your .vimrc or somewhere else (see |vim-addon-manager-plugin_sources|).

Related work: ~

PATHOGEN:
    Until now I thought it was only about manipulating runtimepath.
    But the most recent announcement about mirroring all vim scripts
    shows that it may become a competitive VAM alternative?
    http://www.vim.org/scripts/script.php?script_id=2332
    http://vim-scripts.org/index.html

ZyX branch:
    http://github.com/ZyX-I/vim-addon-manager
    See |marc-weber-vim-addon-manager-design-notes|

There are also some other package managers for vim:
    http://github.com/c9s/Vimana
    http://snk.tuxfamily.org/log/vim-script-management-system.html
    http://github.com/gmarik/vundle (git support only, VimL)
You can try and see which is the best.

another list: http://vim-scripts.org/tools.html

Emacs install scripts manager:
  el-get http://github.com/dimitri/el-get/blob/master/el-get.el

There is another project which has the same name:
http://packages.debian.org/sid/vim-addon-manager
The author (Jamessan) is fine with this project sharing the same name.

------------------------------------------------------------------------------
7.1. Author contacts                                *vim-addon-manager-author*

Github account: MarcWeber
Email: marco-oweber@gmx.de

------------------------------------------------------------------------------
7.2. Contributors                             *vim-addon-manager-contributors*

(Incomplete list):
Tim Pope (+)  (tpope on github)
  - Json validation
ZyX (+) (Nikolay Pavlov, ZyX-I on github)
  - enhancing this vim documentation
  - various fixes
  - discussing implementation details
  - initial implementation for updating plugins which were installed by
    archive
  - pushing 7z and providing a reference implementation
  - smart strip components implementation
Tux Tom (+) (http://github.com/TuxTom)
  - helping fixing a cd bug on Windows
creidiki (Leonardo Valeri Manera)
  - greatly improving Windows support
  - working around the tar issues on Windows
..
Mike Smullin:
 - for asking to find a way to source addons when gui is running.
   There is a hint now. This is important if you don't start vim from console

Israel Chauca Fuentes:
 - reporting and fixing some bugs

(+) means they are collaborators on github and could take action in case of serious
    trouble.

------------------------------------------------------------------------------
ROADMAP:

What features are missing?

- When updating archive based plugins create diffs so that you can patch
  those files without changes being lost. I expect users to customize mappings
  and such

- add a command like AddonManagerFetchUtils which does all the work listed in
  9. for you putting the tar etc into a directory and adding it to PATH

- suggestions?


==============================================================================
9. Testing this plugin                             *vim-addon-manager-testing*

Run >
    # Replace ~/.vim/vim-addons/vim-addon-manager with the path you installed this
    # plugin to
    cd ~/.vim/vim-addons/vim-addon-manager
    sh vim-addon-manager-test.sh
.
You should see >
    1
    1
    1
at the end. Written for Linux only. Code coverage is bad. Its a starting
point.

==============================================================================
10. Some notes for windows users                   *vim-addon-manager-windows*

Finally the nightmare came to an end:
http://mawercer.de/~marc/vam/index.php
Enter plugins names, download final .zip. Be happy and enjoy VAM.

OLD docs:

Win32 tools for vim-addon-manager

Fast: put curl into ~/.vim/vim-addons/binary-utils/dist then call this function
to setup the remaining tools (tar, bzip, gzip, unzip)
>
    call vam#install#FetchAdditionalWindowsTools()
<
If 7z is in PATH bzip, unzip, gzip are not pulled. So consider installing that
and add it to PATH by
>
    let $PATH=$PATH.';'.PATH_TO_7z
<
( TODO: test it, provide a way to install 7z instead? )

After the tools are setup, sometimes installing an addon still fail.
Usually this is triggered by a weird value of |shellslash| or |shellquote|.
Here is that was found to be working:
>
    set shellslash=0
    set shellquote=\"
<

See http://stackoverflow.com/questions/355988/how-do-i-deal-with-quote-characters-when-using-cmd-exe
for more informations.

Ashwin Nanjappa provided an alternative installation instruction here which
worked for him. You may still want to read this section first. There are
multiple ways to make VIM run on Windows and install all missing dependencies.
http://choorucode.wordpress.com/2010/12/09/using-vim-addon-manager-on-windows/

Does it make sense to support svn and git this way as well?
Is it even possible?
At least we can fetch and execute the installers..

Section provided by creidiki:

Curl:
    Get Curl here: http://curl.haxx.se/download.html

    1. Scroll down to the end of the "Packages" list. You want (ideally) the
       latest version, of type `binary' (not libcurl or devel). SSL is not
       necessary but won't hurt. If your OS is 64-bit, you can get the Win64
       version if you want, but it won't be any faster.
    2. Install it. Then point vim-addon-manager (and, incidentally, netrw) to
       it by putting something like this in your _vimrc: >
           let g:netrw_http_cmd='{path_to_curl}\curl.exe -o'
<      (replace {path-to-curl} with the installation directory, including the
       brackets).

    Don't forget the -o argument at the end. :)

Archives:
    Get Tar, Gzip and Unzip from the GnuWin32 project
    Gzip: http://gnuwin32.sourceforge.net/packages/gzip.htm
    Tar: http://gnuwin32.sourceforge.net/packages/gtar.htm
    Unzip: http://gnuwin32.sourceforge.net/packages/unzip.htm

    Either get the complete package setups, or the binary installers. All you
    need is the binaries.
    Install or unpack them somewhere.

    Get xzdec here: http://nullprogram.com/blog/2010/04/16/
    or here: http://www.stats.ox.ac.uk/pub/Rtools/goodies/xzutils.zip
    You only need xzdec.exe. Put in the same place you put those other archive
    tools.

    Get p7zip here: http://7-zip.org/download.html. You need the full version
    with gui because package with command-line version contains only 7za
    executable, while you need 7z in order to support more archives.

    Either add them to your system or user path (google it!) or add them just
    to vim's path by putting something like this in _vimrc: >
        let $PATH='{path-to-gnu-utils};'.$PATH
<   Again, replace the whole of {path-to-gnu-utils} including brackets with
    the directory you have installed/unpacked them to.

VCS:
    MSysGit (and TortoiseGit/GitExtensions) by default add the `git'
    executable to your PATH. If you chose not to for whatever reason, you can
    use the trick above to let vim see them. TortoiseHG does the same with the
    mercurial `hg' executable. TortoiseSVN does not include the commandline
    `svn' executable, so if you need it install SilkSVN

    SilkSVN: http://www.sliksvn.com/en/download/
    MsysGit: http://code.google.com/p/msysgit/
    Mercurial: http://mercurial.selenic.com/


We tried thinking about making using vim-addon-manager easier for Windows
users. One idea was introducing a proxy which asks a linux server to package
git/svn/mercurial/bazaar as zip file on the fly. This would mean you don't
have to install svn,hg,.. to use plugins only.

If you want something like this to happen contact me.

==============================================================================
11. Some notes for Gentoo users                     *vim-addon-manager-gentoo*

Gentoo users may consider installing this plugin from pluginloader overlay
which is accessible via mercurial, url:
http://vimpluginloader.hg.sourceforge.net:8000/hgroot/vimpluginloader/pluginloader-overlay

In order to use this overlay paludis users can add the pluginloader.conf file
to /etc/paludis/repositories/ with the following contents: >
    location = /var/paludis/repositories/pluginloader
    format = e
    sync = hg+http://vimpluginloader.hg.sourceforge.net:8000/hgroot/vimpluginloader/pluginloader-overlay

    master_repository = gentoo
    names_cache = ${location}/.cache/names
    write_cache = /var/cache/paludis/metadata

Then you should unmask app-vim/vim-addon-manager and run >
    paludis --sync && paludis --install vim-addon-manager
or >
    cave sync && cave resolve -x vim-addon-manager
and everything will probably be fine.

For portage+layman users:
1. Create a file /var/lib/layman/pluginloader-overlay.xml with the following
   contents: >
        <?xml version="1.0" encoding="UTF-8"?>
        <!DOCTYPE repositories SYSTEM "/dtd/repositories.dtd">
        <repositories xmlns="" version="1.0">
          <repo quality="experimental" status="unofficial">
            <name>pluginloader-overlay</name>
            <description>Overlay that contains some additional vim plugins</description>
            <source type="mercurial">http://vimpluginloader.hg.sourceforge.net:8000/hgroot/vimpluginloader/pluginloader-overlay</source>
          </repo>
        </repositories>
2. Add the following line to /etc/layman/layman.cfg just after the line that
   starts with `overlays' (do not forget to indent it, see comment above the
   target line): >
                file:///var/lib/layman/pluginloader-overlay.xml
3. Run >
        layman -ka pluginloader-overlay
4. Unmask vim-addon-manager and run >
        emerge vim-addon-manager


==============================================================================
*marc-weber-vim-addon-manager-design-notes*

Until now I tried following the "New Jersey", KISS, MIT principles:
- Try to achieve 80% of value by spending 20% of effort
- I consider this piece of code being perfect if there is nothing you can
  remove.
- If it behaves ok in 95% of all use cases its good enough (for me)
  because I prefer a minimal code base.

ZyX created a branch which tries to be accurate summarrized by quoting him:
"I am trying to get rid of as much future errors as possible".
See related work to get it instead of this. Lets watch it and be curious about
what's going to happen.

==============================================================================
12. Troubleshooting and known bugs        *vim-addon-manager-trouble-shooting*

  runtimepath manipulation should be separated from installation / update
  logic. This way you can have different implementation for updating plugins
  eg one being written in python which you can run from commandline.
  However to keep it portable this means we had to support
  - a VimL solution (eg for Windows users ?)
  - a python/ruby/perl/whatsoever commandline version
  (See thread "which plugin management system to use?" on vim_use)

  ERROR: Vim(call):E117: Unknown function: vam#Activate

  SOLUTION: set runtimepath (rtp) correctly. It must match the directory you
            installed VAM into.

  ANY OTHER ERROR:
  Try commenting all your code in .vimrc and other plugins - does it work
  then? Settings which are known to cause trouble:

    :echo &shell
    :echo &shellslash (some windows users seem to set this differently?)

[*]: Comment about calling ActivateAddons() using autocommands:

  Obviously GUIEnter is only an option if you use gvim only.

  Some plugins may fail to assign their changes to existing buffers
  this way. That's one of the reasons why calling ActivateAddons()
  using an autocommand is marked as experimental.
  Probably those are I "won't fix" bugs. Eventually consider patching
  those plugins instead.

  Workaround: Try adding this to to ActivateAddons() function which should
  catch almost all issues by reassigning the filetype to each buffer: >
  bufdo exec 'set ft='.&ft
< (or use the tlib#buffer#BufDo() function from tlib which will try to
  preserve the current buffer ... or ... - This may have undesired
  side effects: Try "vim small-file 2gb-file.txt" and vim will startup fast
  because the 2gb file will be loaded only if you switch to the buffer.
  Eg try :bn and you'll wait several minutes. Using bufdo will force loading
  the (all) buffers on command line immediately ..).
  You get the idea why I'm not going to maintain this. Putting it into your
  .vimrc should be easy enough.

[2]: VAM's behaviour depends on PATH
  If you don't have git in PATH git sources are dropped. ZyX comitted it
  making me somewhat unhappy. However a warning is echoed so its ok.
  This means that VAM falls back to vim.sf.net if possible if you don't have
  git installed.
  I should fix this by making this filtering explicit by asking users to put
  this into their ~/.vimrc:
  let g:vim_addon_manager.filter_scms = filter(['git','svn','bzr','hg'], '!executable(v:val)')
  If you feel strong about it comment the closed bug on the github bug
  tracker.


TODOs:
- support vim-scripts.org. This will let you get all www.vim.org scripts using
  git repositories only. I think the general idea is great. However we have to
  take care to keep patches which were written for git/hg in sync.

  If you want this feature tell me. I'll add the sources to
  |vim-addon-manager-known-repositories| then.

- there should be a AddonInfo command or such.
  A draft implementation can be found here:
  https://github.com/MarcWeber/vim-addon-manager/issues/41

Sometimes it happens that some small bugs appear. Most of the time they are
easy to identify and trivial fix. Usually fixes happen very fast (within less
than 2 days)
If you don't want to wait try running >
  git checkout HEAD^
< in the vim-addon-manager directory up to 10 times to checkout older
revisions and retry.

tianyicui : Actually I think the name [vim-addon-manager] is too long
(github)    and totally uncool.
Marc Weber: Your suggestion VAM as abbreviation is fine. Let's use that.
            Switching the name means a lot of work and many dead links.
            I'm not going to switch the name for 'uncoolness' reasons soon.

Don't have those commands? See |vim-addon-manager-installation|.
Windows installation is more tedious than on Linux - yes.

Still trouble? Get in touch, see |vim-addon-manager-author|

The help file should be using VAM. (Yes - but its too much work to get it
right for now)

You have to trust script authors:
Because everybody can upload everything to the repositories (in particular
www.vim.org) you must trust authors. I can't review all code - so expect code
to steal your passwords, run sudo rm -fr / ... etc. (This never happened to
me. But it could)
Briefly: I'm not responsible for the code you get using vim-addon-manager.
You can use |InstallAddons| to preview source.

If something goes wrong - and if I don't reply within 24h:
The following github users are collaborators. This means they have write
access to the official repository:
tpope, dertuxmalwieder, TuxTom.

If a plugin behaves strangely then try
http://github.com/dahu/VimLint/blob/master/plugin/vimlint.vim.
Maybe you have an uncommon Vim setting causing some plugins to fail.

There is currently no way forcing loading a plugin at .vimrc time.
Vim delays executing plugin/*.vim files until .vimrc finished processing.
This leads to some hacks such as defining g:in_load_known_repositories.
Forcing loading a plugin earlier (including dependencies !) would allow us to
get rid of this hack.

Its a bug that VAM supports ',' in path items of runtimepath.
Most other plugins do not. IMHO the true fix would be fixing Vim:
call add(g:runtimepath, "newpath") and all the trouble would be gone.
But vim does not use g:runtimpath, it uses an option with strange quoting.
Same applies to errorformat.
So don't use ',' in paths and everyone is happy.

I only act uppon feedback. So if you want changes you have to contact me.
Don't expect me to know what you dream about.

"I want feature X" such as browsable lists of plugins. ActivateAddons FOO
make launching the plugin permanent, ...
Me too - pay me.
Seriously: I don't want to add all features to VAM. It is designed as minimal
as possible. This excludes any gui features. If you want to add such browser
or the like create a new plugin "vim-addon-manager-gui" people can install and
use. I'm happy to put a link into this manual or assist you. However
I personally don't have time to write it.

VAM only checks out 2 revisions using git.
Its not a bug. You can get remaining versions by git fetch --depth=9999999
or such. This speeds up installation very much and should be still be
sufficient to prepare patches.


==============================================================================
13. VAM vs ...

vs Pathogen 
-------------

  1) helptags is run automatically always when updating or installing plugins
    only

  2) Because there is a maintained pool of plugins you can patch or
    deprecate them:
    This happened to Sanders snipmate.
    Most patches just add "root" key telling VAM to add a subdir to rtp.

  3) simple dependency management

  4) supports all common VCS (svn, bazaar, git, mercurial)

  5) Automatic downloading of scripts from vim.org.

  6) It works around some incomplete implementations of rtp in Vim.
    Eg Vim does not load after and ftdetect files of rtp paths.

    update: https://github.com/tpope/vim-pathogen/issues/29
    indicates that Pathogen will cope with ftdetect in the future

  7) You can easily load plugins at runtime.


vs X
---------
  TODO: Most people are using VAM or Pathogen now AFAIK - so if you feel need
  for this list submit patches

I personally don't have anything against the other solutions. I like to point
out the importance of the "dependency" feature. It allows reusing code easily.
Thus code will be reused easily. Thus code review will take place yielding
more collaboration thus better code. That was the main reason why I started
VAM. And that's good for community.

==============================================================================
20. making plugins work with VAM (rtp different from ~/.vim)

Most plugins should work out of the box.

Some older plugins expect to be installed into ~/.vim. Probably they can be
patched easily by replacing ~/.vim by a path relative to the file running the
code: >

  " this is ~/.vim/vim-addons/foo/autoload/x.vim
  " does not work inside a function!
  let s:plugin_base_dir = expand('<sfile>:p:h:h')
  " now s:plugin_base_dir contains ~/.vim/vim-addons/foo
<
Then plugins can find additional resources (templates, python files, ..)
relative to s:parent_dir_of_this_file

==============================================================================
The linux script is gone. Search for "unix based os" to learn about its
replacment.

See :h 'modeline' to learn about the following line setting up defaults.
Usually you'd prefer ftplugin files (do it once).
vim:tw=78:fo=tcq2:ts=8:ft=help:norl:bt=help
